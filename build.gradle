buildscript {
    repositories {
        jcenter()
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath "com.google.appengine:gradle-appengine-plugin:1.9.49"
        classpath "nu.studer:gradle-credentials-plugin:1.0.1"
    }
}

ext {
    appengineVersion = "1.9.49"
    chuckNorrisVersion = "1.0.1"
    commonsCollectionVersion = "4.1"
    commonsLangVersion = "3.5"
    guiceVersion = "4.1.0"
    hamcrestVersion = "1.3"
    junitVersion = "4.12"
    mockitoVersion = "1.10.19"
    restfbmessengerVersion = "1.2.0"
    rivescriptVersion = "0.8.1"
    servletVersion = "2.5"
    slf4jVersion = "1.7.22"
}

description = "Chuck Norris IO App for Facebook Messenger"

apply plugin: "java"
apply plugin: "idea"
apply plugin: "war"
apply plugin: "appengine"
apply plugin: "nu.studer.credentials"

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
}

dependencies {
    appengineSdk "com.google.appengine:appengine-java-sdk:${appengineVersion}"
    compile "com.github.marsbits.restfbmessenger:restfbmessenger-core:${restfbmessengerVersion}"
    compile "com.google.appengine:appengine-api-1.0-sdk:${appengineVersion}"
    compile "com.google.inject:guice:${guiceVersion}"
    compile "com.google.inject.extensions:guice-servlet:${guiceVersion}"
    compile "com.rivescript:rivescript-core:${rivescriptVersion}"
    compile "io.chucknorris:client-java:${chuckNorrisVersion}"
    compile "org.apache.commons:commons-collections4:${commonsCollectionVersion}"
    compile "org.apache.commons:commons-lang3:${commonsLangVersion}"
    compile "org.slf4j:slf4j-jdk14:${slf4jVersion}"
    providedCompile "javax.servlet:servlet-api:${servletVersion}"
    testCompile "junit:junit:${junitVersion}"
    testCompile("org.hamcrest:hamcrest-core:${hamcrestVersion}")
    testCompile("org.hamcrest:hamcrest-library:${hamcrestVersion}")
    testCompile "org.mockito:mockito-core:${mockitoVersion}"
}

appengine {
    disableUpdateCheck = true
    downloadSdk = true
    httpPort = 8088
    appcfg {
        noCookies = true
        oauth2 = true
    }
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

war {
    filesMatching("**/appengine-web.xml") {
        expand([
            "appengineAppId": "chucknorris-io-fb-messenger",
            "facebookVerifyToken": credentials.chucknorrisIoVerifyToken,
            "facebookAccessToken": credentials.chucknorrisIoAccessToken,
            "facebookAppSecret": credentials.chucknorrisIoAppSecret
        ])
    }
}

task wrapper(type: Wrapper) {
    description = "Generates gradlew[.bat] scripts."
    gradleVersion = "3.3"
}
